HeyGrammar {
  Prog = (Def | comment)* Val
  Def = "def" identifier colon? Prog
  Val = Literal | Result

  Result = Call (lpar Val* rpar)*
    
  Call = Range | Square | Concat | Repeat | Slice | Function | Known
  Range = "range" lpar Val Val Val rpar
  Square = "square" lpar Val Val rpar
  Concat = ("concat" | "c") lpar Val* rpar
  Repeat = ("repeat" | "r") lpar Val Val rpar
  Slice = ("slice" | "s") lpar Val Val Val? rpar
  Function = lpar identifier* rpar arrow? Prog

  Literal = number | string | color 
  Known = identifier
    
  identifier = ~reserved name
  name = &letter (alnum | "-")+
  number = sign? digit+
  sign = "+" | "-"
  color = "green" | "blue" | "yellow" | "red" | "purple" | "grey" | "black" | "white"
  lpar = "("
  rpar = ")"
  colon = ":"
  arrow = "->" | ":"
  reserved = ("range" | "square" | "def" | "concat" | "c" | "repeat" | "r" | "slice" | "s" | color) ~name
  string = "\"" (~"\"" any | "\"\"")* "\""
  comment = ";" (~eol any)* eol
  eol = "\n" | "\r" | "\u2028" | "\u2029"
}
