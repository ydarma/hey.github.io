HeyGrammar {
  Prog = (Def | comment)* Val
  Def = "def" identifier colon? Prog
  Val = Literal | Result

  Result = Call (lpar Val* rpar)*
    
  Call = Range | AdaLovelace | Square | Concat | Repeat | Slice | Function | Known
  Range = "range" ~name
  AdaLovelace = "ada-lovelace" ~name
  Square = "square" ~name
  Concat = ("concat" | "c") ~name
  Repeat = ("repeat" | "r") ~name
  Slice = ("slice" | "s") ~name
  Function = lpar identifier* rpar arrow? Prog

  Literal = number | string | color 
  Known = identifier | system
    
  identifier = ~reserved name
  name = &letter (alnum | "-")+
  number = sign? digit+
  sign = "+" | "-"
  color = "green" | "blue" | "yellow" | "red" | "purple" | "grey" | "black" | "white"
  lpar = "("
  rpar = ")"
  colon = ":"
  arrow = "->" | ":"
  reserved = ("def" | system | color) ~name
  system = ("range" | "ada-lovelace" | "square" | "concat" | "c" | "repeat" | "r" | "slice" | "s") ~name
  string = "\"" (~"\"" any | "\"\"")* "\""
  comment = ";" (~eol any)* eol
  eol = "\n" | "\r" | "\u2028" | "\u2029"
}
